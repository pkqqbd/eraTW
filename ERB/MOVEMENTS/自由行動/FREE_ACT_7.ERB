@EXIST_FreeAct7
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct7(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
;陪同行動指令の名称
CASE "名称"
	IF Activity_Duration:ARG > 30
		CALLF MAKE_STR(V_NAME, "幫忙做飯")
	ELSE
		CALLF MAKE_STR(V_NAME, "求分點飯菜")
	ENDIF
CASE "CHAIN"
;他角色に連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;陪同行動指令での経過時間
;0だと30分か残り時間全部の小さい方
	IF Activity_Duration:TARGET > 30
		経過時間 = Activity_Duration:TARGET - 30
	ELSE
		経過時間 = 0
	ENDIF
	CALLF MAKE_INT(V_NAME, 経過時間)
ENDSELECT

@自由行動内容_7(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"	
		;料理技能1以上の角色が空腹状態で廚房にいる時
		SIF ABL:CHARA:料理技能 < 1
			RETURN 0
		SIF !KITCHEN(現在地)
			RETURN 0
		SIF TIME_PROGRESS(TCVAR:CHARA:空腹時刻) <= 0
			RETURN 0
		IF メッセージ表示
			IF CHAIN
				IF Activity_Duration:CHARA > 30
					PRINTFORMW %CALLNAME:CHARA%決定給%CALLNAME:CHAIN%幫忙了。
				ELSE
					PRINTFORMW %CALLNAME:CHARA%讓%CALLNAME:CHAIN%給自己分了點吃的。
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:CHARA%覺得肚子餓了、開始做飯了。
			ENDIF
		ENDIF
		RETURN 60 + RAND:30
	CASE "行動中"
		;吃飯とセット
		IF Activity_Duration:CHARA > 30
			SIF メッセージ表示
				PRINTFORMW %CALLNAME:CHARA%在輕快地做著飯。
		ELSE
			SIF メッセージ表示
				PRINTFORMW %CALLNAME:CHARA%在吃自己做好的飯菜。
			CALL 自由行動吃飯(CHARA)
		ENDIF
	CASE "完了"
		;完了までしたら料理経験+2
		EXP:CHARA:料理経験 += 2
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%似乎吃完飯了。
		CALL 自由行動吃飯(CHARA)
	CASE "中断"
		EXP:CHARA:料理経験 += 1
		SIF Activity_Duration:CHARA <= 30
			CALL 自由行動吃飯(CHARA)
	CASE "交手"
		;自由行動に付き合ったとき用メッセージ
		IF TFLAG:193 == 2
			PRINTFORML %CALLNAME:PLAYER%幫%CALLNAME:TARGET%一起做飯
			SELECTCASE ABL:TARGET:料理技能
			CASE IS <= 1
				PRINTFORML %CALLNAME:TARGET%的料理實在是無法置評…
			CASE 2, 3
				PRINTFORML %CALLNAME:TARGET%正再慢慢地一步步地做著飯菜…
			CASEELSE
				PRINTFORML %CALLNAME:TARGET%在手腳利落地做著飯菜…
			ENDSELECT
			PRINTFORML %CALLNAME:PLAYER%讓%CALLNAME:TARGET%給自己分了些做好的飯菜
		ELSEIF TFLAG:193 < 0
			PRINTFORML 其實也沒那麼餓…
		ELSE
			PRINTFORML 肚子多少填滿了…
		ENDIF

	CASE "ソース"
		IF Activity_Duration:TARGET > 30
			;交手指令選択時の入手ソース、時間係数は付き合った時間の1/10
			DOWNBASE:MASTER:体力 += 20 * 時間係数
			DOWNBASE:MASTER:気力 += 20 * 時間係数
			DOWNBASE:TARGET:体力 += 10 * 時間係数
			DOWNBASE:TARGET:気力 += 10 * 時間係数
			SOURCE:歓楽 = (100 + ((ABL:従順 + ABL:MASTER:料理技能) * 50)) * 時間係数
			TFLAG:98 = 1
			EXP:MASTER:料理経験 ++
			TFLAG:193 = 2
		ELSE
			CALL FreeActMeal(TARGET, 時間係数)
		ENDIF
ENDSELECT