;幫忙掃除
@EXIST_FreeAct4
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct4(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
CASE "名称"
	CALLF MAKE_STR(V_NAME, "幫忙掃除")
CASE "CHAIN"
;他角色に連鎖する場合コメントアウト外すこと
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;陪同行動指令での経過時間
;未入力だと最大30分
	;CALLF MAKE_INT(V_NAME, 経過時間)
ENDSELECT

@自由行動内容_4(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"
		;個室にいる時
		SIF !BEDROOM(現在地)
			RETURN 0
		SIF BASE:CHARA:気力 <= 500
			RETURN 0
		;汚臭耐性（掃除係）によって積極性が変わる
		IF TALENT:CHARA:汚臭耐性 < 0 || TALENT:CHARA:掃除係
			SIF YOGORE:(現在地) < 2000
				RETURN 0
		ELSEIF TALENT:CHARA:汚臭耐性 == 0
			SIF YOGORE:(現在地) < 3500
				RETURN 0
		ELSEIF TALENT:CHARA:汚臭耐性 > 0
			SIF YOGORE:(現在地) < 5000
				RETURN 0
		ENDIF
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%\@ CHAIN ? 決定幫%CALLNAME:CHAIN%一起打掃除了。# 似乎在意房間髒污、決定打掃除了。\@
		RETURN 30 + RAND:20
	CASE "行動中"
		;実際に掃除
		YOGORE:(現在地) = MAX(YOGORE:(現在地) - 250 * TIME_PROGRESS(TIME:1) / 5, 0)
		IF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%%TEXTR("清扫着垃圾/整理和服品/打扫着灰尘/在手腳利落地收拾著房間")%。
		ENDIF
	CASE "完了"
		;清掃経験が入る
		EXP:CHARA:清掃経験 += 3
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%似乎掃除完畢了。
	CASE "中断"
		;中断されても清掃経験は入る
		EXP:CHARA:清掃経験 += 1
	CASE "交手"
		PRINTFORML %CALLNAME:PLAYER%幫助%CALLNAME:TARGET%%TEXTR("將整理好的垃圾一起拿出去扔掉了/將佔滿灰塵的地方一起用抹布擦乾淨了/一起把一個人伸手夠不到的地方打掃乾淨了")%
	CASE "ソース"
		LOCAL = YOGORE:(CFLAG:MASTER:現在位置)
		YOGORE:(CFLAG:MASTER:現在位置) = MAX(YOGORE:(CFLAG:MASTER:現在位置) -100 * 経過時間 / 5, 0)
		EX:MASTER:今日的掃除回数 += LOCAL - YOGORE:(CFLAG:MASTER:現在位置)
		DOWNBASE:MASTER:体力 += 20 * 時間係数
		DOWNBASE:MASTER:気力 += 40 * 時間係数
		DOWNBASE:TARGET:体力 += 10 * 時間係数
		DOWNBASE:TARGET:気力 += 20 * 時間係数
		SOURCE:歓楽 = (100 + ((ABL:従順 + ABL:MASTER:清掃技能) * 50)) * 時間係数
		TFLAG:98 = 2
		EXP:MASTER:清掃経験 ++
		EXP:TARGET:清掃経験 ++
ENDSELECT