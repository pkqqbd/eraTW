@EXIST_FreeAct12
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct12(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
;陪同行動指令の名称
CASE "名称"
	CALLF MAKE_STR(V_NAME, "一起放鬆")
CASE "CHAIN"
;他角色に連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;陪同行動指令での経過時間
;0だと30分か残り時間全部の小さい方
	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@自由行動内容_12(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"
		;昼間に休憩できる場所または憩いスペースにいる時
		SIF !INRANGE(TIME, 600, 960)
			RETURN 0
		SIF !休憩可(現在地) && !憩いスペース(現在地)
			RETURN 0
		;炬燵が発生するならそっちを優先する
		SIF 炬燵発生可(CHARA, 現在地)
			RETURN 0
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%\@ CHAIN ? 被%CALLNAME:CHAIN%吸引也跟著一起放鬆了。# 緩了口氣在一邊休息起來。\@
		RETURN 60 + RAND:30
	CASE "行動中"
		IF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%%TEXTR("正在打哈欠/正在放松/正在发呆/正在悠閒自在地放著輕鬆")%。
		ENDIF
	CASE "完了"
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%似乎休息完了。
	CASE "中断"
	CASE "交手"
		;自由行動に付き合ったとき用メッセージ
		PRINTFORML %CALLNAME:PLAYER%和%CALLNAME:TARGET%一起%TEXTR("發著呆、/懶懶散散地/悠閒自在地")%渡過了一段時間…
	CASE "ソース"
		;交手指令選択時の入手ソース、時間係数は付き合った時間の1/10
		CALL RECOVER_PERMIL(MASTER, 30, "体力", 1)
		CALL RECOVER_PERMIL(MASTER, 80, "気力", 1)
		CALL RECOVER_PERMIL(TARGET, 50, "体力", 1)
		CALL RECOVER_PERMIL(TARGET, 100, "気力", 1)
		SOURCE:歓楽 = (100 + ((ABL:親密 + 好感度係数(TARGET)) * 10)) * 時間係数
		SOURCE:受動 = (50 + ((ABL:従順 + 好感度係数(TARGET)) * 5)) * 時間係数
ENDSELECT