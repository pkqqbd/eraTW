@EXIST_FreeAct13
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct13(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
;陪同行動指令の名称
CASE "名称"
	CALLF MAKE_STR(V_NAME, "陪同喝酒")
CASE "CHAIN"
;他角色に連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;陪同行動指令での経過時間
;0だと30分か残り時間全部の小さい方
	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@自由行動内容_13(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"
		;酒飲める角色が夜間に憩いスペースにいる時
		SIF TALENT:CHARA:酒耐性 < 0
			RETURN 0
		SIF !INRANGE(TIME, 1200, 1440) && !INRANGE(TIME, 0, 120)
			RETURN 0
		SIF !憩いスペース(現在地)
			RETURN 0
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%\@ CHAIN ? 決定陪%CALLNAME:CHAIN%一起喝酒了。# 拿出了燒酒開始了一個人的酒會。\@
		RETURN 60 + RAND:30
	CASE "行動中"
		;稍微ずつ酔う,酔いすぎるとSHIRAHUではなくなり中断されるので注意
		BASE:CHARA:酒気 += TIME_PROGRESS(TIME:1) * (RAND:3 + 1) * (4 - TALENT:CHARA:酒耐性)
		IF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%%TEXTR("一边喝酒一边笑了起来/慢慢品尝着酒的味道/沉浸在醉醺醺的气氛中/在閒情逸致地喝著酒")%。
		ENDIF
	CASE "完了"
		BASE:CHARA:酒気 += TIME_PROGRESS(TIME:1) * (RAND:3 + 1) * (4 - TALENT:CHARA:酒耐性)
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%似乎結束了饮酒。
	CASE "中断"
		BASE:CHARA:酒気 += TIME_PROGRESS(TIME:1) * (RAND:3 + 1) * (4 - TALENT:CHARA:酒耐性)
	CASE "交手"
		;自由行動に付き合ったとき用メッセージ
		PRINTFORML %CALLNAME:PLAYER%陪%CALLNAME:TARGET%喝著燒酒、互相碰了碰杯子
		SELECTCASE TALENT:TARGET:酒耐性
		CASE 0, 1
			PRINTFORML 在溫熱美味的燒酒之下、兩人愉快地談笑風生…
		CASE 2
			PRINTFORML 實在是很讓人心醉的好酒啊…
		CASEELSE
			PRINTFORML 是過於強烈的酒了…
		ENDSELECT
	CASE "ソース"
		;交手指令選択時の入手ソース、時間係数は付き合った時間の1/10
		;相手の酒耐性によって飲む酒が変わる,ソースも相手の酒耐性依存,な酒耐性减少は晩酌しない
		DOWNBASE:MASTER:体力 += 20 * 時間係数
		DOWNBASE:MASTER:気力 += 10 * 時間係数
		CALL DRUNKEN(MASTER, MAX(50 + TALENT:TARGET:酒耐性, 51))
		SOURCE:歓楽 = (150 + ((ABL:親密 + TALENT:TARGET:酒耐性) * 50)) * 時間係数
		SOURCE:受動 = (100 + ((ABL:従順 + TALENT:TARGET:酒耐性) * 50)) * 時間係数
		SOURCE:征服 = (100 + ((ABL:施虐属性 + TALENT:TARGET:酒耐性) * 50)) * 時間係数
		TFLAG:98 = 1
ENDSELECT