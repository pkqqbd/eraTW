@EXIST_FreeAct14
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct14(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
;陪同行動指令の名称
CASE "名称"
	CALLF MAKE_STR(V_NAME, "幫忙买東西")
CASE "CHAIN"
;他角色に連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;陪同行動指令での経過時間
;0だと30分か残り時間全部の小さい方
	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@自由行動内容_14(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"
		;お店にいる時
		SIF !KAIDASHI_SHOP(現在地) && !FLOWER_SHOP(現在地) && !SAKE_SHOP(現在地) && !YASAI_SHOP(現在地)
			RETURN 0
		SIF BASE:CHARA:気力 <= 500
			RETURN 0
		SIF !INRANGE(TIME, 540, 1140)
			RETURN 0
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%\@ CHAIN ? 決定跟著%CALLNAME:CHAIN%一起購物了。# 拿出了購物袋來了。\@
		RETURN 60 + RAND:30
	CASE "行動中"
		IF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%%TEXTR("寻找着便宜的商品/拿起物品開始估值/在烦恼着该买哪个/在隨意地逛街購物中")%。
		ENDIF
	CASE "完了"
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%似乎買完東西了。
	CASE "中断"
	CASE "交手"
		;自由行動に付き合ったとき用メッセージ
		PRINTFORML %CALLNAME:PLAYER%陪%CALLNAME:TARGET%一起逛街購物了
		PRINTFORML %TEXTR(@"%CALLNAME:TARGET%询问着要买哪一款…/被%CALLNAME:TARGET%告知要寻找类似的商品…/提著%CALLNAME:TARGET%的購物袋到處移動中…")%
	CASE "ソース"
		;交手指令選択時の入手ソース、時間係数は付き合った時間の1/10
		DOWNBASE:MASTER:体力 += 20 * 時間係数
		DOWNBASE:MASTER:気力 += 20 * 時間係数
		DOWNBASE:TARGET:体力 += 10 * 時間係数
		DOWNBASE:TARGET:気力 += 10 * 時間係数
		SOURCE:歓楽 = (200 + ((ABL:従順 + 好感度係数(TARGET))  * 50)) * 時間係数
		TFLAG:98 = 1
ENDSELECT