;-----------------------------------------------------------
;【新游戏特典】
;-----------------------------------------------------------
@NEWGAME_BONUS_SELECT
#DIMS buttonText, 10
SETCOLOR 103,228,126
PRINTFORML 【新遊戲特典】
RESETCOLOR
PRINTFORML 請選擇你喜歡的一項
buttonText:0 '= "￥50000"
buttonText:1 '= "初期好感度・信賴度UP"
buttonText:2 '= "初期能力UP"
buttonText:3 '= "獨佔愛慾的奇跡之石"
CALL ASK_BUTTONS(buttonText)
SELECTCASE RESULT
	CASE 0
		新游戏特典 '= "￥50000"
	CASE 1
		CALL NEWGAME_BONUS_CHARA
		IF !RESULT
			RESTART
		ENDIF
	CASE 2
		新游戏特典 '= "初期能力UP"
	CASE 3
		新游戏特典 '= "スケベセット"
ENDSELECT
PRINTFORMW 選択了%新游戏特典%
RETURN

;-----------------------------------------------------------
;初期好感度
;-----------------------------------------------------------
@NEWGAME_BONUS_CHARA
PRINTFORML 任意一位角色的初期好感度+500、信賴度+50
PRINTFORML 請選擇對象
CALL ASK_CALLNAME
IF RESULT == 999
	RETURN 0
ELSE
	新游戏特典 '= TOSTR(RESULT)
	RETURN 1
ENDIF

;-----------------------------------------------------------
;特典適用
;未指定の場合は所持金
;-----------------------------------------------------------
@NEWGAME_BONUS_APPLY
#DIM C_ID
C_ID = TOINT(新游戏特典)
SELECTCASE 新游戏特典
	CASE "無"
	CASE ""
		MONEY += 50000
	CASE "￥50000"
		MONEY += 50000
	CASE "初期能力UP"
		ABL:MASTER:清掃技能 = 1
		ABL:MASTER:教養 = 1
		ABL:MASTER:料理技能 = 1
		ABL:MASTER:技巧 = 2
		ABL:MASTER:話術技能 = 3
	CASE "スケベセット"
		ITEM:奇跡之石 += 1
		ITEM:跳蛋 += 1
		ITEM:媚薬 += 5
		MAXBASE:MASTER:TSP = 500
		BASE:MASTER:TSP = MAXBASE:MASTER:TSP
		MONEY:1 = 1800
		ABL:MASTER:技巧 = 2
	CASEELSE
		CFLAG:C_ID:好感度 = 500
		CFLAG:C_ID:信賴度 = 100
		IF CHECK_CHARA(C_ID, "神霊")
			TALENT:MASTER:讓神霊懷孕 = 1
		ELSEIF CHECK_CHARA(C_ID, "付喪神")
			TALENT:MASTER:讓付喪神懷孕 = 1
		ELSEIF CHECK_CHARA(C_ID, "蓬莱人")
			TALENT:MASTER:讓蓬莱懷孕 = 1
		ELSEIF CHECK_CHARA(C_ID, "幽霊")
			TALENT:MASTER:讓幽霊懷孕 = 1
		ELSEIF CHECK_CHARA(C_ID, "人形")
			TALENT:MASTER:讓人偶懷孕 = 1
		ENDIF
ENDSELECT

;-----------------------------------------------------------
;設定表示用文本化
;-----------------------------------------------------------
@NEWGAMEBONUS_TEXT
#FUNCTIONS
SELECTCASE 新游戏特典
	CASE ""
		RETURNF "<font color='#67e47e'>" + "可以選択新游戏特典" + "</font>"
	CASE "￥50000"
		RETURNF "\\以50000金钱为初始資金開局"
	CASE "初期能力UP"
		RETURNF "一部份能力的上昇使得早期的家事更容易"
	CASE "スケベセット"
		RETURNF "一部份能力的上昇使得早期的Ｈ更容易"
	CASEELSE
		RETURNF @"%CALLNAME:TOINT(新游戏特典)%的初期好感度与信賴度上升的状態下開局 对于难以妊娠的種族、也会追加確率提昇的能力"
ENDSELECT
