;-----------------------------------------------------------
;角色メイク
;全角色追加後に行う
;-----------------------------------------------------------
@NEWGAME_MAKE
;共通初期化
CALL NEWGAME_MAKE_INIT
;モード選択
CALL NEWGAME_MAKE_SELECT_MODE
;ステータス設定

;-----------------------------------------------------------
;初期化
;-----------------------------------------------------------
@NEWGAME_MAKE_INIT
;NOは随意設定
NO:MASTER = 0
;MASTERは初期性別＝男
;性別(1=女性 2= 男性 3=扶她)
TALENT:MASTER:性別 = 2
;気力、体力
MAXBASE:MASTER:体力 = 2000
BASE:MASTER:体力 = MAXBASE:MASTER:0
MAXBASE:MASTER:気力 = 2000
BASE:MASTER:気力 = MAXBASE:MASTER:1
;TSP
MAXBASE:MASTER:TSP = 100
BASE:MASTER:TSP = 100

;-----------------------------------------------------------
;モード選択
;-----------------------------------------------------------
@NEWGAME_MAKE_SELECT_MODE
CLEARLINE 10000
PRINTL ★★请选择游戏模式★★
CALL ASK_CHOICES10("START","扮演模式（一部分口上无法显示）")
SELECTCASE RESULT
	CASE 0
		FLAG:游戏模式 = 0
		CALL NEWGAME_MAKE_ANATA
	CASE 1
		FLAG:游戏模式 = 1
		CALL NEWGAME_MAKE_NARIKIRI
		SIF !RESULT
			RESTART
ENDSELECT

;-----------------------------------------------------------
;通常
;-----------------------------------------------------------
@NEWGAME_MAKE_ANATA
PRINTW 在游戏開始前你可以自定义你的角色。
CALL CUSTOM_TERMINAL(MASTER)

;-----------------------------------------------------------
;扮演
;-----------------------------------------------------------
@NEWGAME_MAKE_NARIKIRI
#DIM nNo
PRINTL 
PRINTL 要变成誰呢？
CALL ASK_CALLNAME
IF RESULT == 999
	RETURN 0
ELSEIF !IS_NAMED(RESULT)
	RESTART
ENDIF
nNo = RESULT
PRINTFORMW 作为%CALLNAME:nNo%開始游戏

;扮演コピー
NAME:MASTER = %NAME:nNo%
CALLNAME:MASTER = %CALLNAME:nNo%
FOR LOCAL,0,200
	TALENT:MASTER:LOCAL = TALENT:nNo:LOCAL
NEXT
FOR LOCAL,0,55
	ABL:MASTER:LOCAL = ABL:nNo:LOCAL
NEXT
FOR LOCAL,0,14
	MAXBASE:MASTER:LOCAL = MAXBASE:nNo:LOCAL
	BASE:MASTER:LOCAL = MAXBASE:nNo:LOCAL
NEXT
FOR LOCAL,0,100
	EXP:MASTER:LOCAL = EXP:nNo:LOCAL
NEXT
;体力気力底上げ
IF MAXBASE:MASTER:体力 < 2000
	PRINTFORML 這個角色的体力很低。要提高到2000麼？
	CALL ASK_YN("提高","就這樣")
	SIF RESULT == 0
		MAXBASE:MASTER:体力 = 2000
ENDIF
IF MAXBASE:MASTER:気力 < 2000
	PRINTFORML 這個角色的気力很低。要提高到2000麼？
	CALL ASK_YN("提高","就這樣")
	SIF RESULT == 0
		MAXBASE:MASTER:気力 = 2000
ENDIF
;TSP
MAXBASE:MASTER:TSP = 100
BASE:MASTER:TSP = 100
;処女
IF !TALENT:MASTER:処女
	PRINTL 要变成処女吗？
	CALL ASK_YN("是","否")
	IF RESULT == 0
		CALL SET_VIRGIN(MASTER)
	ENDIF
ENDIF

;CFLAG:MASTER:初期位置 = CFLAG:nNo:初期位置
FLAG:扮演 = nNo
CFLAG:nNo:出禁 = 1
CALL CUSTOM_INIT(MASTER)
RETURN 1

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;服装の設定
;CALL		USER
;ARG		MASTER角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@MASTER_CLOTHES_INIT
IF FLAG:扮演
	CFLAG:MASTER:基本服装セット = FLAG:扮演 + 100
ELSEIF TALENT:MASTER:性別 & 1
	CFLAG:MASTER:基本服装セット = 2
ELSE
	CFLAG:MASTER:基本服装セット = 1
ENDIF

@MASTER_CLOTHES
#DIMS choices, 100
;列表作成
VARSET choices
FOR LOCAL, 0, 10
	IF LOCAL >= 7
		choices:LOCAL '= MASTER_CLOTHES_NAME(LOCAL+1)
	ELSE
		choices:LOCAL '= MASTER_CLOTHES_NAME(LOCAL)
	ENDIF
NEXT
choices:99 '= MASTER_CLOTHES_NAME(99)
;表示
PRINTL
PRINTL
PRINTFORML 請選擇平時的装扮。
DRAWLINE
CALL ASK_CHOICES(choices, 100)
SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 0 TO 99
		CALL MASTER_CLOTHES_SET(RESULT)
		SIF RESULT
			RETURN 1
	CASE 99
		CALL MASTER_CLOTHES_SET(99)
			RETURN 1
ENDSELECT
RESTART

@MASTER_CLOTHES_SET(ARG)
SELECTCASE ARG
	CASE 0
		PRINTFORMW 太过于暴露啦！
		RETURN 0
	CASE 1 TO 6
		CFLAG:MASTER:基本服装セット = RESULT
	CASE 7 TO 9
		CFLAG:MASTER:基本服装セット = RESULT + 1
	CASE 99
		CALL MASTER_CLOTHES_INIT
	CASEELSE
		THROW 指定了無効的番号。
ENDSELECT
PRINTFORMW 服装変更为%MASTER_CLOTHES_NAME(CFLAG:MASTER:基本服装セット)%
RETURN 1

@MASTER_CLOTHES_NAME(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 0
		RETURNF "全裸"
	CASE 1
		RETURNF "一套男装"
	CASE 2
		RETURNF "一套女装"
	CASE 3
		RETURNF "一套執事服"
	CASE 4
		RETURNF "一套女僕服"
	CASE 5
		RETURNF "一套神官服"
	CASE 6
		RETURNF "一套作務衣"
	CASE 7
		THROW
	CASE 8
		RETURNF "一套道士服"
	CASE 9
		RETURNF "一套学生服"
	CASE 10
		RETURNF "一套女学生服"
	CASEELSE
		RETURNF CALLNAME:MASTER + "の默认服装"
ENDSELECT
THROW