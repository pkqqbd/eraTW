;借书屋
@COM620
#DIM CHARISMA
#DIM ItemId

$C_LOOP
DRAWLINE
IF !CHARISMA
	PRINTFORML 所持金:%金額表示(MONEY)%
ELSE
	PRINTFORML 所持筹码:{MONEY:2}
ENDIF
PRINTL 「借书每本一律\3000」
DRAWLINE
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	PRINTFORM [{LOCAL}] %ITEMNAME:ItemId%
	IF ITEM:ItemId
		PRINTFORML （被%CALLNAME:MASTER%借着）
	ELSEIF GETBIT (FLAG:1011,LOCAL)
		PRINTL （借出中）
	ELSE
		PRINTL 　
	ENDIF
NEXT

IF !CHARISMA
	PRINTFORML [100] 籌碼付款
ELSE
	PRINTFORML [100] 現金支付
ENDIF
DRAWLINE
PRINTL [0] 放棄

$LOOP
INPUT
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	IF ITEM:ItemId
		PRINTL 「不能同时借二冊以上」
		RETURN -1
	ENDIF
NEXT

IF RESULT == 100
	IF !CHARISMA
		PRINTFORMW 「籌碼付款？？？？」
		PRINTFORMW 「嗯……那么就10个籌碼？可以么……」
		IF MONEY:2 < 10
			PRINTFORMW 籌碼不足
			GOTO C_LOOP
		ENDIF
	ELSE
		PRINTFORMW 「啊、我用钱支付吧」
	ENDIF
	PRINTFORML 
	CHARISMA = !CHARISMA
	GOTO C_LOOP
ELSE
	IF !CHARISMA && MONEY < 3000
		PRINTW お金が足りなかった…
		RETURN -1
	ENDIF
	IF INRANGE(RESULT, 1, BookTitles)
		IF GETBIT (FLAG:1011,RESULT)
			LOCALS = 「正在外借中呢」
			IF CFLAG:[[小鈴]]:現在位置 == CFLAG:PLAYER:現在位置
				CALL SPTALK, [[小鈴]], "通常", 0, @"%LOCALS%", 1
				SIF TARGET != [[小鈴]]
					RESETCOLOR
			ELSE
				CALL SPTALK, 0, "通常", 12, @"%LOCALS%", 1
			ENDIF
		ELSE
			SETBIT FLAG:1011,RESULT
			IF !CHARISMA
				MONEY -= 3000
			ELSE
				MONEY:2 -= 10
			ENDIF
			ITEM:(100 + RESULT) = 1
			FLAG:借书返还期限 = 8
			TFLAG:193 = 1
			PRINTFORML 借走了%ITEMNAME:(100 + RESULT)%
			LOCALS = 「非常感谢」/「还书期限是一週哦」
			IF CFLAG:[[小鈴]]:現在位置 == CFLAG:PLAYER:現在位置
				CALL SPTALK, [[小鈴]], "笑顔", 0, @"%LOCALS%", 1
				SIF TARGET != [[小鈴]]
					RESETCOLOR
			ELSE
				CALL SPTALK, 0, "笑顔", 12, @"%LOCALS%", 1
			ENDIF
			RETURN 1
		ENDIF
	ELSEIF RESULT == 0
		RETURN -1
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
TIME += 30

;借书屋
@COM_ABLE620
;一括管理
SIF GLOBAL_COMABLE(620)
	RETURN RESULT
;借书屋ではない場所
SIF !BOOK_RENTAL(CFLAG:MASTER:現在位置)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

;借书屋を判定する関数
@BOOK_RENTAL(ARG)
#FUNCTION
;鈴奈庵
SIF ARG == GET_MAP_REPLACEMENT(213)
	RETURNF 1
RETURNF 0

@BOOKLEND
FOR LOCAL,1,BookTitles + 1
	SIF LOCAL == 6	;[紙芝居]をスルー
		CONTINUE
	SIF !ITEM:(100 + LOCAL) && !RAND:3
		INVERTBIT FLAG:1011,LOCAL
NEXT
SIF FLAG:借书返还期限
	FLAG:借书返还期限 -= 1
IF FLAG:借书返还期限 == 1
	FOR LOCAL,1,BookTitles + 1
		ITEM:(100 + LOCAL) = 0
	NEXT
	PRINTL 因为还书时限到了、把借來的书归还了。
ENDIF
