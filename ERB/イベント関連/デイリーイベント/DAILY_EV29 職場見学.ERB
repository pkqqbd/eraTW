@DAILY_CONDITION_EV29
SIF !K63_KENGAKU
	RETURN -1
CALL DAILY_EV29
RETURN 1

@DAILY_EV29
K63_KENGAKU = 0
FLAG_DAILY_EV29_LASTTIME = DAY
CFLAG:[[影狼]]:最後に来訪した日 = DAY
CFLAG:[[若鷺姫]]:最後に来訪した日 = DAY
CFLAG:[[蛮奇]]:最後に来訪した日 = DAY
IF !GETBIT(FLAG_DAILY_EV29, 0)
	CALL DAILY_EV29_0
ELSE
	CALL DAILY_EV29_1
ENDIF
RETURN 1

;初回用
@DAILY_EV29_0
CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
PRINTFORMW 「来啦ー」
CALL PRINT_FACE, [[若鷺姫]], "通常", "服", "", ""
PRINTFORMW 「今天很高兴认识你」
CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
PRINTFORML 「这身打扮怎么样？是不是很像人类？」
CALL ASK_YN("相对来说","大概不会暴露")
CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
PRINTFORMW 「是吧？那么、去蛮奇那里吧！」
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
CALL PRINT_FACE, [[若鷺姫]], "通常", "服", "", ""
PRINTFORMW 「不错的店啊」
CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
PRINTFORMW 「店员服看起来好棒」
CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
PRINTFORMW 「诶……为什么在这里……」
PRINTFORML 「是%CALLNAME:MASTER%做的吗」
PRINTFORML 「为什么会把这些家伙也带来啊」
IF TALENT:[[影狼]]:恋慕
	CALL ASK_M("因为想见蛮奇",1,"既然是影狼的请求就不能拒绝",1,"请给我一份煮杂烩粥",1)
ELSE
	CALL ASK_M("因为想见蛮奇",1,"因为被影狼拜托了",1,"请给我一份这个煮杂烩粥",1)
ENDIF
SELECTCASE RESULT
	CASE 0
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORML 「这不是把她们带来的理由吧」
		PRINTFORML 「嘛算啦、快点吃完然后回去吧」
		CALL CHANGE_CFLAG(2,[[蛮奇]],50)
		CALL CHANGE_CFLAG(4,[[蛮奇]],10)
	CASE 1
		IF TALENT:[[影狼]]:恋慕
			CALL PRINT_FACE, [[影狼]], "発情", "服", "", ""
			PRINTFORML 「真是的、突然说这种话、真是的……」
			CALL CHANGE_CFLAG(2,[[影狼]],100)
			CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
			PRINTFORMW 「影狼好麻烦」
		ELSE
			CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
			PRINTFORML 「也考虑一下我的情况吧」
			PRINTFORML 「嘛算啦、快点吃完然后回去吧」
		ENDIF
	CASE 2
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「那个现在没有」
		PRINTFORMW 「不要摆出世界末日了的表情啊、点些别的东西吧」
		DAILY_EV29_NIKOMI ++
ENDSELECT
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
CALL BUFF_BASE(MASTER,BASE_体力,150,1)
CALL BUFF_BASE(MASTER,BASE_気力,150,1)
CALL BUFF_BASE([[影狼]],BASE_体力,150,1)
CALL BUFF_BASE([[影狼]],BASE_気力,150,1)
CALL BUFF_BASE([[若鷺姫]],BASE_体力,150,1)
CALL BUFF_BASE([[若鷺姫]],BASE_気力,150,1)
CALL PRINT_FACE, [[若鷺姫]], "通常", "服", "", ""
PRINTFORMW 「好美味啊」
PRINTFORMW 「啊对了、需要花钱的」
CALL PRINT_FACE, [[若鷺姫]], "笑顔", "服", "", ""
PRINTFORMW 「我没有带啊！」
CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
PRINTFORMW 「我也是！」
CALL 画像セット(@"顔絵_服_通常_{[[影狼]]}", 0, 0, 100, 0, 0, "", "默认顔絵")
CALL 画像セット(@"顔絵_服_通常_{[[若鷺姫]]}", 0, 0, 100, 0, 0, "", "默认顔絵")
CALL 画像一斉表示("左", 0, 1)
PRINTFORMW 两人一起看向了%CALLNAME:MASTER%
CALL COLORMESSAGE(@"所持金:%金額表示(MONEY)%",C_YELLOW,1)
CALL ASK_M("垫付（\10000）", MONEY >= 10000, "看向蛮奇",1)
SELECTCASE RESULT
	CASE 0
		MONEY -= 10000
	CASE 1
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「为什么看着我啊」
		CALL PRINT_FACE, [[蛮奇]], "憤怒", "服", "", ""
		IF MONEY >= 10000
			PRINTFORMW 「你不是有足够吗！付钱！」
			MONEY -= 10000
		ELSE
			CALL EV29_無銭飲食
		ENDIF
ENDSELECT

IF !FLAG_DAILY_EV29_DEBT
	CALL PRINT_FACE, [[若鷺姫]], "笑顔", "服", "", ""
	PRINTFORMW 「万分感谢！」
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORMW 「下次会补偿的」
	CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
	PRINTFORMW 「不要再来了」
	;通い狼に影響
	CFLAG:[[影狼]]:1004 += 3
	CFLAG:[[影狼]]:1006 ++
	FLAG_DAILY_EV29_PAID ++
	CALL CHANGE_CFLAG(2,[[影狼]],100)
	CALL CHANGE_CFLAG(4,[[影狼]],50)
	CALL CHANGE_CFLAG(2,[[若鷺姫]],100)
	CALL CHANGE_CFLAG(4,[[若鷺姫]],50)
	CALL CHANGE_CFLAG(2,[[蛮奇]],100)
	CALL CHANGE_CFLAG(4,[[蛮奇]],50)
ELSE
	CALL CHANGE_CFLAG(2,[[影狼]],50)
	CALL CHANGE_CFLAG(2,[[若鷺姫]],50)
ENDIF
SETBIT FLAG_DAILY_EV29, 0


;二回目以降
@DAILY_EV29_1
#DIM PAYMENT
#DIM C_ID
#DIM TOUCH
TOUCH = 0
IF GROUPMATCH(TIME:5, 4, 5, 6)
	CALL PRINT_FACE, [[若鷺姫]], "笑顔", "服", "", ""
	PRINTFORMW 「今天下雨所以游泳来了」
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORMW 「如果一直是这样的话就轻松多了」
ELSEIF !RAND:2
	CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
	PRINTFORMW 「啊啦、让你久等了？」
	CALL PRINT_FACE, [[若鷺姫]], "通常", "服", "", ""
	PRINTFORMW 「今天好早呢」
ELSEIF !RAND:4
	CALL PRINT_FACE, [[若鷺姫]], "通常", "服", "", ""
	PRINTFORMW 「今天是%CALLNAME:MASTER%会推我吗？」
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORMW 「因为用湿毛巾裹着所以有些重吧？」
ELSE
	CALL PRINT_FACE, [[若鷺姫]], "通常", "服", "", ""
	PRINTFORMW 「晚上好」
	CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
	PRINTFORMW 「好、我们出发吧」
ENDIF
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
IF !RAND:5
	CALL SPTALK, [[影狼]], "通常", 0, @"「哼哼……我觉得今天的%TEXTR("肉/魚/野菜/山菜")%很美味」", 1
	CALL SPTALK, [[若鷺姫]], "通常", 0, @"「影狼酱不要再闻味道啦」", 1
ELSE
	IF !RAND:2
		CALL SPTALK, [[若鷺姫]], "通常", 0, @"「晚上好ー」", 1
	ELSE
		CALL SPTALK, [[若鷺姫]], "通常", 0, @"「今天的人比较%TEXTR("多/少")%呢」", 1
	ENDIF
	CALL SPTALK, [[影狼]], "通常", 0, @"「%TEXTR("我又来了哟ー/这个髪型很棒呢/讷今天姫的打扮是不是很可愛？")%」", 1
ENDIF

CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
IF FLAG_DAILY_EV29_DEBT > 0
	PRINTFORML 「今天你带了钱吧」
ELSEIF !RAND:10
	PRINTFORML 「你们不会是专门看时间才来的吧、明明已经换班次了……」
ELSEIF !RAND:5
	PRINTFORML 「即使是腌制也想要吗……」
PRINTFORML （原文“ぶぶ漬け出してやりたい”出自京都的歇后语、意为“该吃晚饭了、回家吧。”）
ELSE
	PRINTFORML 「又是你们啊」
ENDIF
PRINTFORML 「要点些什么？」
C_ID = 0
TOUCH = 0
$ORDER
CALL ASK_M("本日のおすすめ",1,"影狼と同じやつ",1,"若鷺姫と同じやつ",1,"この煮込み雑燴粥一つください",1, "影狼にボディタッチ", TOUCH >= 0)
;CALL ASK_M("本日のおすすめ",1,"影狼と同じやつ",1,"若鷺姫と同じやつ",1,"この煮込み雑燴粥一つください",1, "パーッといく(\30000)",MONEY >= 30000)
SELECTCASE RESULT
	CASE 0
		C_ID = [[蛮奇]]
	CASE 1
		C_ID = [[影狼]]
	CASE 2
		C_ID = [[若鷺姫]]
	CASE 3
		DAILY_EV29_NIKOMI ++
		IF DAY:2 == 2
			PRINTFORMW 「即使天气这么热也想要吗」
		ELSEIF DAILY_EV29_NIKOMI >= 5 && !RAND:5
			PRINTFORML 「%CALLNAME:MASTER%はそればっかね」
			PRINTFORML 「真的有那么好吃吗、那个」
			CALL ASK_YN("好吃","特别好吃")
			IF !RESULT
				PRINTFORML 「呼姆ー」
			ELSE
				PRINTFORML 「你就是这样的人吧」
			ENDIF
		ENDIF
	CASE 4
		SELECTCASE TOUCH
			CASE 0
				PRINTFORML 机の陰で、影狼の手に手を重ねた
				IF !TALENT:[[影狼]]:恋慕 || RAND:400 > CFLAG:[[影狼]]:积攒度
					PRINTFORMW 影狼は%TEXTR("一瞬、戸惑ったような視線を送ってきた/無反応を装った")%
				ELSE
					PRINTFORMW 影狼は%TEXTR("一瞬視線を送ってきた/重ねた手に、もう片方の手をさらに載せてきた")%
					CFLAG:[[影狼]]:积攒度 += 100
				ENDIF
				CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
				PRINTFORML 「まだ決まらないの？」
				TOUCH = 1
			CASE 1
				PRINTFORML 影狼の膝に手を置いた
				IF !TALENT:[[影狼]]:恋慕
					PRINTFORMW 手の甲に爪を立てられた…
					TOUCH = -1
				ELSEIF RAND:600 > CFLAG:[[影狼]]:积攒度
					PRINTFORMW 支付のけられた…
					TOUCH = -1
				ELSE
					CALL PRINT_FACE, [[影狼]], "通常", "服", "照れ"
					PRINTFORMW 影狼も%CALLNAME:MASTER%の膝に手を置いてきた…
					CFLAG:[[影狼]]:积攒度 += 100
					CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
					PRINTFORML 「おい……」
					TOUCH = 2
				ENDIF
			CASE 2
				PRINTFORMW 影狼の内腿を悄悄的撫でる
				CALL PRINT_FACE, [[影狼]], "発情", "服", "", ""
				PRINTFORML 影狼はお返しとばかりに
				CALL SPTALK, [[蛮奇]], "憤怒", 0, @"「いい加減にしろ！」", 1
				TOUCH = -1
		ENDSELECT
		GOTO ORDER
ENDSELECT
IF C_ID
	CALL CHANGE_CFLAG(2,C_ID,100)
	CALL CHANGE_CFLAG(4,C_ID,50)
ENDIF
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
CALL BUFF_BASE(MASTER,BASE_体力,150,1)
CALL BUFF_BASE(MASTER,BASE_気力,150,1)
CALL BUFF_BASE([[影狼]],BASE_体力,150,1)
CALL BUFF_BASE([[影狼]],BASE_気力,150,1)
CALL BUFF_BASE([[若鷺姫]],BASE_体力,150,1)
CALL BUFF_BASE([[若鷺姫]],BASE_気力,150,1)
PAYMENT = 0
IF FLAG_DAILY_EV29_DEBT > 0
	CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
	PRINTFORML 「上次的也一起交清吧」
	CALL COLORMESSAGE(@"所持金:%金額表示(MONEY)%　花费:%金額表示(10000)%　借金:%金額表示(FLAG_DAILY_EV29_DEBT)%",C_YELLOW,1)
	CALL ASK_M("全部还清", MONEY >= 10000 + FLAG_DAILY_EV29_DEBT, "请饶了我吧、只付今天的账单", MONEY >= 10000,"赊账",1)
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW 「不错」
			MONEY -= (10000 + FLAG_DAILY_EV29_DEBT)
			FLAG_DAILY_EV29_DEBT = 0
			FLAG_DAILY_EV29_PAID ++
			CALL CHANGE_CFLAG(2,[[蛮奇]],100)
			CALL CHANGE_CFLAG(4,[[蛮奇]],50)
			PAYMENT = 1
		CASE 1
			PRINTFORMW 「快点还钱」
			MONEY -= 10000
			FLAG_DAILY_EV29_PAID ++
			PAYMENT = 1
		CASE 2
			CALL PRINT_FACE, [[蛮奇]], "憤怒", "服", "", ""
			PRINTFORML 「你在開玩笑吗」
			FLAG_DAILY_EV29_DEBT += 10000
	ENDSELECT
ELSE
	CALL SPTALK, [[蛮奇]], "通常", 0, @"「结账」", 1
	CALL COLORMESSAGE(@"所持金:%金額表示(MONEY)%　花费:%金額表示(10000)%",C_YELLOW,1)
	IF !GETBIT(FLAG_DAILY_EV29, 1)
		CALL ASK_M("支付", MONEY >= 10000, "不支付",1)
	ELSE
		CALL ASK_M("支付", MONEY >= 10000, "赊账",1)
	ENDIF
	SELECTCASE RESULT
		CASE 0
			MONEY -= 10000
			PAYMENT = 1
		CASE 1
			CALL EV29_無銭飲食
	ENDSELECT
ENDIF
;ちゃんと払った
IF PAYMENT
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORML 「承蒙款待！」
	CALL PRINT_FACE, [[若鷺姫]], "笑顔", "服", "", ""
	PRINTFORML 「谢谢ー！」
	CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
	PRINTFORMW 「你们不要理所当然的装傻啊」
	;通い狼に影響
	CFLAG:[[影狼]]:1004 += 3
	CFLAG:[[影狼]]:1006 ++
	FLAG_DAILY_EV29_PAID ++
	CALL CHANGE_CFLAG(2,[[影狼]],100)
	CALL CHANGE_CFLAG(4,[[影狼]],50)
	CALL CHANGE_CFLAG(2,[[若鷺姫]],100)
	CALL CHANGE_CFLAG(4,[[若鷺姫]],50)
	CALL CHANGE_CFLAG(2,[[蛮奇]],100)
	CALL CHANGE_CFLAG(4,[[蛮奇]],50)
	IF !RAND:10
		CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
		PRINTFORML 「今天是不是有一点点咸了」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「烦死啦」
	ELSEIF !RAND:5
		CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
		PRINTFORML 「我还会再带肉过来的」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「拜托请不要带生的」
	ELSEIF !RAND:6
		CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
		PRINTFORML 「顺便说一下、下次的会合是」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「不要在这里说这个」
	ELSEIF !RAND:10
		CALL PRINT_FACE, [[若鷺姫]], "通常", "服", "", ""
		PRINTFORML 「来応援这次的漂流吧」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「如果能去的话」
	ELSE
		CALL PRINT_FACE, [[若鷺姫]], "笑顔", "服", "", ""
		PRINTFORML 「还会再来的」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「别再来了」
	ENDIF
;払ってない
ELSE
	CALL SPTALK, [[蛮奇]], "憤怒", 0, @"「赶快还钱」", 1
	CALL CHANGE_CFLAG(2,[[影狼]],50)
	CALL CHANGE_CFLAG(2,[[若鷺姫]],50)
ENDIF



@EV29_無銭飲食
IF !GETBIT(FLAG_DAILY_EV29, 1)
	CALL PRINT_FACE, [[蛮奇]], "憤怒", "服", "", ""
	PRINTFORMW 「没有钱吗？！怎么回事啊！」
	PRINTFORMW 也许是觉得奇怪、厨房里传出了声音
	CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
	PRINTFORMW 「什么事都没有！」
	PRINTFORMW 「没办法……不足的部分我帮你垫付」
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORMW 「得救了！」
	CALL PRINT_FACE, [[若鷺姫]], "笑顔", "服", "", ""
	PRINTFORMW 「最喜欢蛮奇了！」
	PRINTFORML 
	CALL ASK_YN("很快会还回来的","最喜欢蛮奇了！")
	IF !RESULT
		CALL PRINT_FACE, [[蛮奇]], "憤怒", "服", "", ""
		PRINTFORMW 「理所当然的」
	ELSE
		CALL PRINT_FACE, [[蛮奇]], "憤怒", "服", "", ""
		PRINTFORMW 「闭嘴笨蛋」
	ENDIF
ELSE
	CALL SPTALK, [[蛮奇]], "憤怒", 0, @"「%TEXTR("别得意忘形/你在開玩笑吗/穷鬼")%」", 1
ENDIF
SETBIT FLAG_DAILY_EV29, 1
FLAG_DAILY_EV29_DEBT += 10000
CALL COLORMESSAGE(@"欠蛮奇的借金　類型\\{FLAG_DAILY_EV29_DEBT}",C_YELLOW,2)